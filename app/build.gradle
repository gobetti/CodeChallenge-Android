apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "me.gobetti.codechallenge"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "me.gobetti.codechallenge.TestInstrumentationRunner"
    }
    buildTypes {
        debug {
            buildConfigField 'String', "ApiKey", TMDBApiKey
        }
        release {
            buildConfigField 'String', "ApiKey", TMDBApiKey
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        final String sharedTestDir = 'src/sharedTest'
        final String sharedTestJavaDir = "$sharedTestDir/java"
        final String sharedTestResourcesDir = "$sharedTestDir/resources"
        test {
            java.srcDir sharedTestJavaDir
            resources.srcDir sharedTestResourcesDir
        }
        androidTest {
            java.srcDir sharedTestJavaDir
            resources.srcDir sharedTestResourcesDir
        }
    }
}

def espressoVersion = '3.0.2'
def moshiVersion = '1.6.0'
def retrofitVersion = '2.4.0'
def supportLibraryVersion = '28.0.0-rc01'
def supportTestVersion = '1.0.2'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.paging:runtime:1.0.0"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    implementation 'com.squareup.picasso:picasso:2.71828'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.8.0'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.8.0'

    androidTestImplementation "com.android.support.test:rules:$supportTestVersion"
    androidTestImplementation "com.android.support.test:runner:$supportTestVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'com.android.support') {
            details.useVersion supportLibraryVersion
        }
    }
}